pkgname=webstorm
_pkgname=WebStorm
pkgver=2016.2.1
pkgrel=1
pkgdesc="JavaScript IDE and HTML editor."
arch=('x86_64' 'i686')
options=('!strip')
url="http://www.jetbrains.com/${pkgname}/"
license=('Commercial')
install=${pkgname}.install
optdepends=('java-environment>=8: use system java'
            'java-runtime-common: use system java')
source=(https://download.jetbrains.com/webstorm/${_pkgname}-${pkgver}.tar.gz
        jetbrains-webstorm.desktop
        ${pkgname}.install)
sha512sums=('a0be49ae0a2a61ea2986652e3d7b2a25a6b863bad36f27337691fe1f01df91daa29b7defa63bad08c850c4e55ecf8c4bb61e59a1ee7f1d7d5adea3ad04a82aa9'
            'd963f93c39ae9e3525064d3974ba096074c815ac8d5e7084ec41fb5c07ec46e341a9adafd01356061e84d50431e815cfbffe8c5f7effd087e84380bd6317215f'
            'ea3500f2b37cba930458afe30fb3313d2df3924730c34193f9f90dfcf7449543a33b357fc43e646953cbc258a6e894e5aa6ae12d73f076ceb0880cd4b0a7cbe5')

package() {
  install -d -m 755 ${pkgdir}/opt/
  install -d -m 755 ${pkgdir}/usr/bin/
  install -d -m 755 ${pkgdir}/usr/share/applications/
  install -d -m 755 ${pkgdir}/usr/share/pixmaps/

  wsdir=$(/usr/bin/find ${srcdir} -name 'WebStorm-*' -type d)
  cp -a ${wsdir} $pkgdir/opt/${pkgname}
  # if you want to use system java you may remove the bundled jre and save about 100M
  #rm -rf $pkgdir/opt/${pkgname}/jre

  ln -s /opt/$pkgname/bin/${pkgname}.sh $pkgdir/usr/bin/${pkgname}
  install -D -m 644 ${srcdir}/jetbrains-${pkgname}.desktop ${pkgdir}/usr/share/applications/
  install -D -m 644 "${pkgdir}/opt/${pkgname}/bin/${pkgname}.svg" "${pkgdir}/usr/share/pixmaps/${pkgname}.svg"
}
